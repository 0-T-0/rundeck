version = '2.1.0-SNAPSHOT'
defaultTasks 'clean','build'
apply plugin: 'java'
apply plugin: 'idea'
sourceCompatibility = 1.6
ext.rundeckPluginVersion= '1.1'

configurations{
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile {
        extendsFrom pluginLibs
    }
}
repositories {
    mavenLocal()
    maven{ //snapshot dependency
        url='https://oss.sonatype.org/content/repositories/snapshots/'
    }
    mavenCentral()
}

dependencies {
    compile( [group: 'org.rundeck', name: 'rundeck-core', version: version])
    pluginLibs( [group: 'org.jasypt', name: 'jasypt', version: '1.9.2'])
    testCompile(
        //[group: 'junit', name: 'junit', version: '3.8.1',ext:'jar']
    )
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

ext.pluginClassNames='org.rundeck.plugin.encryption.EncryptionConverterPlugin'
jar {
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion, 'Rundeck-Plugin-Archive': 'true'
        def libList = configurations.pluginLibs.collect { 'lib/' + it.name }.join(' ')
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}



apply plugin: 'maven'
task createPom << {
    pom {
        project {
            artifactId 'rundeck-example-storage-encryption-plugin'
            groupId "org.rundeck.plugins"
            inceptionYear '2014'
            packaging 'jar'
            version version
            name "RunDeck Example Encryption Plugin"
            url 'http://rundeck.org'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            parent{
                groupId 'com.dtolabs.rundeck'
                artifactId "rundeck-bundled-plugins"
                version(version)
            }
            build{
                plugins{
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-jar-plugin'
                        version '2.3.2'
                        configuration{
                            archive{
                              manifestEntries{
                                'Rundeck-Plugin-Classnames'(pluginClassNames)
                                'Rundeck-Plugin-File-Version' version
                                'Rundeck-Plugin-Version' rundeckPluginVersion
                                'Rundeck-Plugin-Archive' 'true'
                              }
                            }
                        }
                    }
                }
            }
        }
    }.writeTo("pom.xml")
}
