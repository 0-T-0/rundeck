/**
 * The Rundeck Launch parent build file
 */

description = "The parent launcher project provides defaults for it's children"

apply from: "${rootDir}/gradle/java.gradle"

eclipse.project.name = "${project.getParent().eclipse.project.name}:launcher"

subprojects{
    apply from: "${rootDir}/gradle/java.gradle"
    defaultTasks 'clean','assemble'
    ext.jettyVersion='6.1.21'
    repositories {
        add(new org.apache.ivy.plugins.resolver.FileSystemResolver()) {
            name = 'localJarsPlus'
            addArtifactPattern "${rootDir}/dependencies/[module]/[ext]s/[module]-[revision](-[classifier]).[ext]"
            addArtifactPattern "${rootDir}/rundeckapp/target/[module]-[revision](-[classifier]).[ext]"
            addArtifactPattern "${rootDir}/plugins/script-plugin/build/libs/[module]-[revision](-[classifier]).[ext]"
            addArtifactPattern "${rootDir}/plugins/stub-plugin/build/libs/[module]-[revision](-[classifier]).[ext]"
            addArtifactPattern "${rootDir}/plugins/localexec-plugin/build/libs/[module]-[revision](-[classifier]).[ext]"
            descriptor = 'optional'
            checkmodified = true
        }
    }
}

task createPom << {
    pom {
        project {
            artifactId 'rundeck-launcher-parent'
            groupId project.group
            inceptionYear '2011'
            packaging 'pom'
            version version
            name "RunDeck Launcher Parent"
            url 'http://rundeck.org'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            modules{
                module 'rundeck-jetty-server'
                module 'launcher'
            }
        }
    }.writeTo("pom.xml")
}
